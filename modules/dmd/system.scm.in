;; system.scm -- Low-level operating system interface.
;; Copyright (C) 2013 Ludovic Court√®s <ludo@gnu.org>
;;
;; This file is part of GNU dmd.
;;
;; GNU dmd is free software; you can redistribute it and/or modify it
;; under the terms of the GNU General Public License as published by
;; the Free Software Foundation; either version 3 of the License, or (at
;; your option) any later version.
;;
;; GNU dmd is distributed in the hope that it will be useful, but
;; WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.
;;
;; You should have received a copy of the GNU General Public License
;; along with GNU dmd.  If not, see <http://www.gnu.org/licenses/>.

(define-module (dmd system)
  #:use-module (system foreign)
  #:use-module (rnrs bytevectors)
  #:export (reboot
            halt
            power-off
            max-file-descriptors))

;; The <sys/reboot.h> constants.
(define RB_AUTOBOOT @RB_AUTOBOOT@)
(define RB_HALT_SYSTEM @RB_HALT_SYSTEM@)
(define RB_POWER_OFF @RB_POWER_OFF@)

(define %libc-reboot
  ;; libc's 'reboot' function as declared in <sys/reboot.h>.
  (let* ((ptr  (dynamic-func "reboot" (dynamic-link)))
         (proc (pointer->procedure int ptr (list unsigned-int))))
    (define (howto->symbol howto)
      (cond ((eqv? howto RB_AUTOBOOT) 'RB_AUTOBOOT)
            ((eqv? howto RB_HALT_SYSTEM) 'RB_HALT_SYSTEM)
            ((eqv? howto RB_POWER_OFF) 'RB_POWER_OFF)
            (else howto)))

    (lambda (howto)
      ;; Block asyncs during the call so 'errno' remains unchanged.
      (let ((err (call-with-blocked-asyncs
                  (lambda ()
                    (or (zero? (proc howto))
                        (errno))))))
        (throw 'system-error "reboot" "~A: ~S"
               (list (strerror err) (howto->symbol howto))
               (list err))))))

(define %libc-errno-pointer
  ;; Glibc's 'errno' pointer.
  (let ((errno-loc (dynamic-func "__errno_location" (dynamic-link))))
    (and errno-loc
         (let ((proc (pointer->procedure '* errno-loc '())))
           (proc)))))

(define (errno)
  "Return the current errno."
  ;; XXX: We assume that nothing changes 'errno' while we're doing all this.
  ;; In particular, that means that no async must be running here.
  (if %libc-errno-pointer
      (let ((bv (pointer->bytevector %libc-errno-pointer (sizeof int))))
        (bytevector-sint-ref bv 0 (native-endianness) (sizeof int)))
      0))

(define (reboot)
  "Perform a hard reset of the system now.  Return #f on failure."
  (%libc-reboot RB_AUTOBOOT))

(define (halt)
  "Halt the system.  Return #f on failure."
  (%libc-reboot RB_HALT_SYSTEM))

(define (power-off)
  "Stop system and switch power off if possible.  Return #f on failure."
  (%libc-reboot RB_POWER_OFF))


(define _SC_OPEN_MAX @_SC_OPEN_MAX@)

(define sysconf
  (let* ((ptr  (dynamic-func "sysconf" (dynamic-link)))
         (proc (pointer->procedure long ptr (list int))))
    (lambda (name)
      "Return the system configuration for NAME."
      (let* ((result (proc name))
             (err    (errno)))
        (if (= -1 result)
            (throw 'system-error "sysconf" "~A: ~S"
                   (list (strerror err) name)
                   (list err))
            result)))))

(define (max-file-descriptors)
  "Return the maximum number of open file descriptors allowed."
  (sysconf _SC_OPEN_MAX))
